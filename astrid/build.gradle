buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.10.+'
    }
}

apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    lintOptions {
        lintConfig file("../lint.xml")
    }

    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 19
    }

    signingConfigs {
        release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            runProguard true
            proguardFile 'proguard.cfg'
            proguardFile getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
        }
    }

    if (project.hasProperty('keyAlias') &&
            project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyPassword')) {
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        buildTypes.release.signingConfig = null
    }
}

android.applicationVariants.all { variant ->
    variant.mergeResources.doLast {
        if (project.hasProperty('gapiKey')) {
            File values = file("${buildDir}/res/all/${variant.dirName}/values/values.xml")
            values.write(
                    values.getText('UTF-8').replaceAll(/<string name="gapi_key">.*<\/string>/, "<string name=\"gapi_key\">${project.property('gapiKey')}</string>"),
                    'UTF-8')
        }
    }
}

dependencies {
    compile group: 'com.android.support', name: 'support-v4', version: '19.1.+'
    compile group: 'com.android.support', name: 'appcompat-v7', version: '19.1.+'

    compile 'net.simonvt.menudrawer:menudrawer:3.0.4@aar'

    compile project(":api")

    compile fileTree(dir: "libs", includes: ["*.jar"])

    compile group: 'com.google.guava', name: 'guava', version: '11.0.1', transitive: false
    compile group: 'com.google.code.gson', name: 'gson', version: '1.7.1', transitive: false
    compile group: 'com.google.api-client', name: 'google-api-client', version: '1.6.0-beta', transitive: false
    compile group: 'com.google.api-client', name: 'google-api-client-extensions', version: '1.6.0-beta', transitive: false
    compile group: 'com.google.api-client', name: 'google-api-client-extensions-android2', version: '1.6.0-beta', transitive: false
    compile group: 'com.google.http-client', name: 'google-http-client', version: '1.6.0-beta', transitive: false
    compile group: 'com.google.http-client', name: 'google-http-client-extensions', version: '1.6.0-beta', transitive: false
    compile group: 'com.google.http-client', name: 'google-http-client-extensions-android2', version: '1.6.0-beta', transitive: false
    compile group: 'com.google.http-client', name: 'google-http-client-extensions-android3', version: '1.6.0-beta', transitive: false
    compile group: 'com.google.oauth-client', name: 'google-oauth-client', version: '1.6.0-beta', transitive: false
    compile group: 'com.google.oauth-client', name: 'google-oauth-client-extensions', version: '1.6.0-beta', transitive: false
}
